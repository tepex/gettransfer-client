apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'de.felixschulze.gradle.hockeyapp'

ext {
    support_version            = "28.0.0"
    multidex_version           = "1.0.3"
    constraint_layout_version  = "2.0.0-alpha2"
    moxy_version               = "1.5.5"
    cicerone_version           = "4.0.2"
    circle_indicator_version   = "1.2.2"
    hockey_version             = "5.1.1"
    firebase_core_version      = "16.0.6"
    firebase_iid_version       = "17.0.4"
    firebase_messaging_version = "17.3.4"
    glide_version              = "4.9.0"
    simple_rating_bar_version  = "1.4.2"
    slf4j_timber_version       = "1.0.1"
    markwon_version            = "2.0.0"
    socket_io_version          = "1.0.0"
    libphonenumber_version     = "8.9.14"
    appmetrica_version         = "3.4.0"
    facebook_version           = "4.38.1"
    leakcanary_version         = "1.6.2"
    anko_version               = "0.10.8"
    flexbox_layout_version     = "1.0.0"
    pageindicator_version      = "1.0.2"
    sentry_version             = "1.7.16"
    appsflyer_version          = "4.8.19"
    installreferrer_version    = "1.0"
    life_cycle_version         = "1.1.1"
    easy_permissions_version   = "2.0.1"
    braintree_version          = "2.22.0"
    drop_in_version            = "3.7.1"
    pinview_version            = "1.4.2"
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION
        targetSdkVersion project.ANDROID_BUILD_SDK_VERSION
        testInstrumentationRunner project.ANDROID_TEST_INSTRUMENTATION_RUNNER
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        config {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if(keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties['KEY_ALIAS']
                keyPassword keystoreProperties['KEY_PASSWORD']
                storeFile file(keystoreProperties['STORE_FILE'])
                storePassword keystoreProperties['STORE_PASSWORD']
            }
        }
    }

    android {
        lintOptions {
            abortOnError false
        }
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            useProguard true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            //applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
            shrinkResources false
        }
    }

    flavorDimensions "type"

    productFlavors {
        dev {
            applicationId "com.kg.gettransfer"

            signingConfig signingConfigs.config

            versionCode getBuildNumber() as Integer
            versionName System.getenv("VERSION_NAME") ?: "2.0"

            manifestPlaceholders = [
                    hockeyAppId   : "1f061f58b7bd42c387ecd8681d30df48",
                    assetlinks    : "[{ \\\"include\\\": \\\"https://stgtr.org/.well-known/assetlinks.json\\\" }]"
            ]

            dimension "type"
        }

        prod {
            applicationId "com.gettransfer"

            signingConfig signingConfigs.config

            versionCode getBuildNumber() as Integer
            versionName System.getenv("VERSION_NAME")

            manifestPlaceholders = [
                    hockeyAppId   : "62939f09f9e0489c8ada1c25d2f30d9d",
                    assetlinks    : "[{ \\\"include\\\": \\\"https://gettransfer.com/.well-known/assetlinks.json\\\" }]"
            ]

            dimension "type"
        }

        /* `prod` emulation for testing */
        home {
            applicationId "com.kg.gettransfer"

            signingConfig signingConfigs.config

            versionCode getBuildNumber() as Integer
            versionName "2.0"

            manifestPlaceholders = [
                    hockeyAppId   : "1f061f58b7bd42c387ecd8681d30df48",
            ]

            dimension "type"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

hockeyapp {
    apiToken = "7381bebd314d4f69b363cd29d4ce6b41" //api token with owner rights
    restoreAllowed = true // Enables restoring function for previous versions
    notes = generateChangeLog() // Generates default notes - feel free to change this
    buildServerUrl = System.getenv("JOB_URL") ?: "Unknown"
    repositoryUrl = System.getenv("GIT_URL") ?: "Unknown"
    variantToApplicationId = [
            devRelease:  "1f061f58b7bd42c387ecd8681d30df48",
            prodRelease:  "62939f09f9e0489c8ada1c25d2f30d9d",
    ]
    variantToReleaseType = [
            devRelease:  "2",
            prodRelease:  "1",
    ]
}

dependencies {
    implementation project(":domain")
    implementation project(":data")
    implementation project(":geo")
    implementation project(":prefs")
    implementation project(':logging')
    implementation project(":remote")
    implementation project(":cache")
    implementation project(":encrypt")
    implementation libs.kotlin
    implementation libs.coroutines
    implementation libs.serialization
    implementation libs.koin_android

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation "com.android.support:multidex:$multidex_version"
    implementation "com.android.support:multidex-instrumentation:$multidex_version"
    implementation "android.arch.lifecycle:extensions:$life_cycle_version"
    // Maps
    implementation libs.psLocation
    implementation libs.psPlaces
    implementation "com.google.android.gms:play-services-maps:$gms_version"
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    // Gson
    implementation libs.gson
    // Moxy
    implementation "com.arello-mobile:moxy:$moxy_version"
    implementation "com.arello-mobile:moxy-app-compat:$moxy_version"
    kapt "com.arello-mobile:moxy-compiler:$moxy_version"

    // Cicerone
    implementation "ru.terrakok.cicerone:cicerone:$cicerone_version"

    // Logger
    implementation libs.timber
    implementation "at.favre.lib:slf4j-timber:$slf4j_timber_version"

    // Hockey
    implementation "net.hockeyapp.android:HockeySDK:$hockey_version"

    //Firebase
    // https://firebase.google.com/support/release-notes/android
    implementation "com.google.firebase:firebase-core:$firebase_core_version"
    //implementation "com.google.firebase:firebase-iid:$firebase_iid_version"
    implementation "com.google.firebase:firebase-messaging:$firebase_messaging_version"

    //Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    //RatingBar with vector images
    implementation "com.github.ome450901:SimpleRatingBar:$simple_rating_bar_version"

    //For markdown
    implementation "ru.noties:markwon:$markwon_version"

    // Socket.IO
    implementation "io.socket:socket.io-client:$socket_io_version"

    //AppMetrica
    implementation "com.yandex.android:mobmetricalib:$appmetrica_version"

    //Facebook
    implementation "com.facebook.android:facebook-core:$facebook_version"

    // Phone number code
    implementation "io.michaelrocks:libphonenumber-android:$libphonenumber_version"
    implementation "com.google.android:flexbox:$flexbox_layout_version"

    testImplementation libs.junit
    testImplementation libs.hamcrest
    testImplementation libs.mockito
    testImplementation "org.koin:koin-test:$koin_version"

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakcanary_version"

    //Anko
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

    implementation "com.romandanylyk:pageindicatorview:$pageindicator_version"

    //sentry
    implementation "io.sentry:sentry-android:$sentry_version"

    //appsflyer
    implementation "com.appsflyer:af-android-sdk:$appsflyer_version@aar"
    implementation "com.android.installreferrer:installreferrer:$installreferrer_version"

    //EasyPermissions
    implementation "pub.devrel:easypermissions:$easy_permissions_version"

    //Vector drawable
    implementation "com.android.support:support-vector-drawable:$support_version"
    implementation "com.android.support:animated-vector-drawable:$support_version"

    //Braintree
    implementation "com.braintreepayments.api:braintree:$braintree_version" //don't update until migration to AndroidX
    implementation "com.braintreepayments.api:drop-in:$drop_in_version" //don't update until migration to AndroidX

    //PinEditText
    implementation "com.chaos.view:pinview:$pinview_version"
}

def getBuildNumber() {
    return System.getenv("BUILD_NUMBER") ?: "1"
}

def getVersionName() {
    return System.getenv("VERSION_NAME") ?: "1.0"
}

def generateChangeLog() {
    def changelog = "  \nbranch: " + System.getenv("GIT_BRANCH") ?: "Unknown"
    changelog += "  \ncommit: " + System.getenv("GIT_COMMIT") ?: "Unknown"
    changelog += "  \n\njob name: " + System.getenv("JOB_NAME") ?: "Unknown"
    changelog += "  \nstarted by: " + System.getenv("BUILD_USER") ?: "Unknown"
    changelog += "  \nbuild number: " + System.getenv("BUILD_NUMBER") ?: "Unknown"
    println changelog
    return changelog
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.getkeepsafe.dexcount'
